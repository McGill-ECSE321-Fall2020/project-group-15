class Application {
}

class Address {
  unique String address_id;
  String addressLine1;
  String addressLine2;
  String city;
  String province;
  String postalCode;
  String country;
}

association {
     0..1 Address address -<@> 1 Customer customer;
}

class User {
    abstract;
  
    unique String email;
    String password;
    autounique user_id;
    String firstname;
    String lastname;
    lazy String phoneNumber;

    class Artist {
        lazy String artistDescription;

    }

    class Customer {
    }

    class Administrator {
    }
}

association {
    1 Application application <@>- * User users;
}

//review
class Review {
  autounique review_id;
  Float rating;
  String comment;
  Boolean wouldReccomend;
}

association {
  1 Customer customer -- * Review reviews;
  1 Artist artist <@>- * Review reviews;
}

// art pieces
class Artwork {
    String name;
    String description;
    Float price;
    Date dateOfCreation;
	Integer numInStock;
    unique Integer artwork_id;
}

association {
    1 Artist artist -- * Artwork artworks;
    1 Application application <@>- * Artwork artworks;
}

// buying art

class Order {
    autounique order_id;
    Float totalPrice;
    Date datePlaced;
    lazy Date orderCompleted;
    enum DeliveryMethod {Pickup, Delivery};
    DeliveryMethod deliveryMethod;
  enum OrderStatus {PlacedOrder, ShippedOrder, ReadyForPickup,  DeliveredOrder, OrderPickedup};
    OrderStatus orderStatus;
}

association {
    0..1 Order order -- 1..* Artwork artworks;
    1 Customer customer -- * Order orders;
    1 Application application <@>- * Order orders;
}